use std
use args

// Show correct usage of the "args" library
const main = {
	var argv = ["obj/demo", "-h", "-n", "5", "--", "-F", "foo", "bar"][:]

	args.init(argv)

	args.setusage("test [-h] [-n N] name…")

	var c
	while((c = args.opt()) != (0 : char))
		match c
		| 'h':
			std.put("help!\n")
		| 'n':
			std.put("N = {}\n", args.earg())
		| _:
			args.usage()
		;;
	;;

	// Print remaining arguments
	for a : args.argv()
		std.put("→ {}\n", a)
	;;

	std.put("run as: {}\n", args.progname())

	std.put("Done, calling usage()…\n")

	args.usage()
}
